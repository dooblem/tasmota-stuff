>D
; debut journee. hotday=false
; si n > 0 and conso < 700 => fin chauffe, hotday=true
; watt<700 and n<3 => UP
; watt >200 and n>0 => DOWN

; Ã  11h. si hotday=false, on force 1res
; si hot. hotday=true. on deverrouille

http=0
preh=0
;has been hot once in day
;dhot=0
lky=0
prd=0
cso=0
i=0

n1=0
n0=0
; arrays of 3 elements
m:r1=0 3
m:r0=0 3

pic=0

; timer used to pause execution a few secs to disable flapping
; if timer is not null. pause
t:tmr=0

;;;;;;;;;;;;;;;;;;;;;;;;;;
>S

; skip if not sun hours (4h-20h). or timer set
if (upsecs%10==0 and tmr==0 and hours>=4 and hours<20)
{

;print %hours% %preh%

if (hours==4 and preh==3)
{
; print Sun day begin. dhot=0 ;; for now it's done in salon crontab
 print Sun day begin.
; dhot=0
}
preh=hours

http=1
http("192.168.1.19" "/status")
http=0

}

;;;;;;;;;;;;;;;;;;;;;;;;;;
>E
; be sure the event is our http req
if http=0 {
 break
}

=#initRes

;print pwr %pwr[2]% %pwr[3]% %pwr[4]%
;print n1=%0n1% r1 %0r1[1]% %0r1[2]% %0r1[3]%
;print n0=%0n0% r0 %0r0[1]% %0r0[2]% %0r0[3]%

; delim power. get 2nd field. comma as limit
lky=gwr("\"power\":" 2 ,)
prd=gwr("\"power\":" 3 ,)
cso=lky+prd

print TEST %lky% <-2490<-1660<-700 >2690>1860>200 cso:%cso% (n0:%0n0% %0r0[1]%-%0r0[2]%-%0r0[3]%) (n1:%0n1% %0r1[1]%-%0r1[2]%-%0r1[3]%)

; mean resistance power: 830

; check if hot
; 750*2=1500 750*3=2250
if ((n0==1 and cso<750) or (n0==2 and cso<1500) or (n0==3 and cso<2250))
{
; dhot=1
 print Cumulus is HOT. Sleep 15m
 tmr=900
 break
}

;;; turn on. off instead

; 3 on
; 830*3=2490
if (lky<-2490 and n1==3)
{
 =>Power2 0
 =>Power3 0
 =>Power4 0
 tmr=60
 break
}
; 2 on
; 830*2=1660
if (lky<-1660 and n1>=2)
{
 =#pwrRndOff
 =#initRes
 =#pwrRndOff
 tmr=60
 break
}
; 1 on
if (lky<-700 and n1>=1)
{
 =#pwrRndOff
 tmr=60
 break
}

;;; turn off. on instead

; 3 off
; 830*3+200=2690
if (lky>2690 and n0==3)
{
 =>Power2 1
 =>Power3 1
 =>Power4 1
 tmr=60
 break
}
; 2 off
; 830*2+200=1860
if (lky>1860 and n0>=2)
{
 =#pwrRndOn
 =#initRes
 =#pwrRndOn
 tmr=60
 break
}
; 1 off
if (lky>200 and n0>=1)
{
 =#pwrRndOn
 tmr=60
 break
}

;;;;;;; subroutines ;;;;;
#initRes
n1=0
n0=0

for i 2 4 1
if pwr[i]==1 {
 r1[n1+1]=i
 n1+=1
} else {
 r0[n0+1]=i
 n0+=1
}
next

;;;
; pick a random res in array
#pwrRndOff
pic=r1[1+rnd(n1)]
=>Power%0pic% 0

;;;
#pwrRndOn
pic=r0[1+rnd(n0)]
=>Power%0pic% 1
