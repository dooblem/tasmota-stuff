>D

; table of linky records: 7 minutes. you should update {print lkys list} bellow as well.
m:lkys=0 7

; linky limit value to turn ON
lkyON=-250
; linky limit value to turn OFF
lkyOFF=100

; seconds to wait after ON/OFF
wait=600

; Other info to fill bellow:
; {influxdb info}
; {lky}

;;;;;;;;;

;pump minutes (permanent var?)
pm=0
;pm daily target (max)
pmt=0
;pump target by month
m:mo=0 12

lky=0
http=0
; timer used to pause execution a few secs to disable flapping
; if timer is not null. pause
t:tmr=0
; previous hour
preh=0

;parameter or index
i=0

; the cumulus is OK. other loads can use energy. ie pool pump
g:cuOk=0

;;;;;;;;;;;;;;;;;;;;;;;;
>G
print Received cuOk=%cuOk%

;;;;;;;;;;;;;;;;;;;;;;;; on script start
>B
mo[1]=1
mo[2]=1
mo[3]=2
mo[4]=3.5
mo[5]=5
mo[6]=9
mo[7]=10
mo[8]=10
mo[9]=8
mo[10]=6
mo[11]=2
mo[12]=1
=#setPmt

;;;;;;;;;;;;;;;;;;;;;;;;
>S

; every 60s: new day...
if upsecs%60!=0 {
 break
}

if (hours==0 and preh==23)
{
 print New day
 pm=0
 =#setPmt
 ; reset cuOk (though it's the responsibility of the tasmota cumulus)
 cuOk=0
}
preh=hours

; increment mins if power on
if pwr[1]==1 {
 pm+=1
}

lky=0
; only get metrics and operate during sun hours
; in non sun hours we will insert zeros
if (time>sunrise and time<sunset)
{
 http=1
 http("192.168.1.19" "/emeter/0")
 http=0
}
; insert into array even at night
lkys=lky

; pump management when timer ok, and sun hours
if (tmr!=0 or time<sunrise or time>sunset)
{
 break
}

; {print lkys list: update to match lkys size}
print pwr:%0pwr[1]% cuOk:%0cuOk% pm:%0pm%<%0pmt% lky:%lky% [%lkys[1]% %lkys[2]% %lkys[3]% %lkys[4]% %lkys[5]% %lkys[6]% %lkys[7]%] <%0lkyON% >%0lkyOFF%

; block to set pump ON
if (pwr[1]==0 and cuOk==1 and pm<pmt)
{
 ;get max. ignore zero values
 lky=-99999
 for i 1 lkys[-1] 1
  if (lkys[i]!=0 and lkys[i]>lky)
  {
   lky=lkys[i]
  }
 next
 print max:%lky%

 if lky<lkyON {
  =#setPump(1)
 }

; block to set pump OFF
} else {
if pwr[1]==1
{
 if (cuOk==0 or pm>pmt)
 {
  =#setPump(0)

 } else {
  ;get min
  lky=99999
  for i 1 lkys[-1] 1
   if (lkys[i]!=0 and lkys[i]<lky)
   {
    lky=lkys[i]
   }
  next
  print min:%lky%

  if lky>lkyOFF {
   =#setPump(0)
  }
 }
}

;;;;;;;;;;;;;;;;;;;;;;;;
>E
; be sure the event is our http req
if http==1 {
 http=0
} else {
 break
}

lky=gwr("\"power\":" 2 ,)

;;;;;;;;;;;;;;;;;;;;;;;;
#setPump(i)
print Set pump %0i%. Wait %0wait%s
=>Power %0i%
tmr=wait

#setPmt
pmt=mo[month]*60
print Pump minutes target today: %0pmt%
